import (
    "std.um"
    "mat.um"
    
    "th.um"
    "canvas.um"
    "window.um"
    "font.um"

    "gfx.um"
    "phys.um"
)

type HUD* = struct {
    font: font::Font
}

const fontSize = 18

fn (hud: ^HUD) setup*(fontName: str) {
    var err: std::Err
    hud.font, err = font::load(fontName, fontSize, .linear)
    std::exitif(err)
}

fn (hud: ^HUD) draw*(state: ^phys::State, camera: ^gfx::Camera) {
    center := th::Vf2{window::w / 2.0, window::h / 2.0}

    // Own spaceship's crosshair
    const crosshairRatio = 0.6
    crosshairSize := crosshairRatio * window::h / 2.0
    
    canvas::drawLine(th::green, {center.x - crosshairSize, center.y}, {center.x + crosshairSize, center.y}, 1)
    canvas::drawLine(th::green, {center.x, center.y - crosshairSize}, {center.x, center.y + crosshairSize}, 1)

    // Other spaceship's crosshair
    const otherCrosshairLen = 1.0
    if screenPoint := camera.project({0.0, 0.0, 0.0}); screenPoint.visible() && camera.pos[0] < 0.0 {
        horzAxis := [2]gfx::ScreenPoint{camera.project({0.0, -otherCrosshairLen,  0.0}), camera.project({0.0,  otherCrosshairLen,  0.0})}
        vertAxis := [2]gfx::ScreenPoint{camera.project({0.0,  0.0, -otherCrosshairLen}), camera.project({0.0,  0.0,  otherCrosshairLen})}

        canvas::drawLine(th::green, horzAxis[0].point, horzAxis[1].point, 1)
        canvas::drawLine(th::green, vertAxis[0].point, vertAxis[1].point, 1)
    }

    // Horizon line
    horizonDir := mat::Vec{state.att[0], 0.0, 0.0}.toAttMat().mulv({0.0, crosshairSize * 0.5, 0.0}) 
    horizonHeight := camera.near * state.att[1]
    if horizonHeight > crosshairSize {
        horizonHeight = crosshairSize
    } else if horizonHeight < -crosshairSize {
        horizonHeight = -crosshairSize
    }
    canvas::drawLine(th::green, center.add({-horizonDir[1], horizonHeight + horizonDir[2]}), center.add({horizonDir[1], horizonHeight - horizonDir[2]}), 1)

    // Yaw line
    yawOffset := -camera.near * state.att[2]
    if yawOffset > crosshairSize {
        yawOffset = crosshairSize
    } else if yawOffset < -crosshairSize {
        yawOffset = -crosshairSize
    }
    canvas::drawLine(th::green, center.add({yawOffset, -crosshairSize * 1.4}), center.add({yawOffset, -crosshairSize * 1.1}), 1)


    // Position
    pos := state.pos
    hud.font.draw(sprintf("X %7.1f\nY %7.1f\nZ %7.1f", pos[0], pos[1], pos[2]), {center.x - 300, center.y + 200}, th::green, 1.0)
    
    // Velocity
    vel := state.rot.transpose().mulv(state.vel)
    hud.font.draw(sprintf("VX %7.2f\nVY %7.2f\nVZ %7.2f", vel[0], vel[1], vel[2]), {center.x - 150, center.y + 200}, th::green, 1.0)

    // Attitude and angular rate
    att := state.att.div(gfx::deg)
    rate := state.rate.div(gfx::deg)

    hud.font.draw(sprintf("%4.1f [%+3.1f]", att[0], rate[0]), {center.x - crosshairSize - 7 * fontSize, center.y - fontSize / 2}, th::green, 1.0)
    hud.font.draw(sprintf("%4.1f [%+3.1f]", att[1], rate[1]), {center.x + crosshairSize + 10, center.y - fontSize / 2}, th::green, 1.0)
    hud.font.draw(sprintf("%4.1f [%+3.1f]", att[2], rate[2]), {center.x - 2 * fontSize, center.y - crosshairSize * 1.4 - 2 * fontSize}, th::green, 1.0)

    // Precise control mode
    if state.precise {
        hud.font.draw("PRECISE", {center.x - 300, center.y + 260}, th::green, 1.0)
    }

    // FPS and help
    hud.font.draw(sprintf("%.0f FPS\nTRANSLATION : WASD/QE\nROTATION    : ARROWS/,.\nPRECISE     : SPACEBAR", 1000.0 / th::delta), {10, 10}, th::white, 1.0)
}

fn (hud: ^HUD) off*(ok: bool) {
    canvas::drawRect(0x000000FF, {0, 0, window::w, window::h})

    if ok {
        msg := "DOCKED - CAMERA OFF"
        msgSize := hud.font.measure(msg)
        hud.font.draw(msg, {window::w / 2 - msgSize.x / 2, window::h / 2 - msgSize.y / 2}, th::green, 1.0)
    } else {
        msg := "NO SIGNAL"
        msgSize := hud.font.measure(msg)
        hud.font.draw(msg, {window::w / 2 - msgSize.x / 2, window::h / 2 - msgSize.y / 2}, th::red, 1.0)
    }
}